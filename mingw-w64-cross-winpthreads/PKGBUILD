# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=winpthreads
_base_ver=12.0.0
_mingw_suff=mingw-w64-cross
pkgname="${_mingw_suff}-${_realname}"
provides=("${_mingw_suff}-${_realname}-git")
conflicts=("${_mingw_suff}-${_realname}-git")
replaces=("${_mingw_suff}-${_realname}-git")
pkgver=12.0.0
pkgrel=1
pkgdesc="MinGW-w64 winpthreads library for cross-compiler"
arch=('aarch64' 'i686' 'x86_64')
url="https://mingw-w64.sourceforge.io/"
license=('custom')
groups=("${_mingw_suff}-toolchain" "${_mingw_suff}")
makedepends=("git" "${_mingw_suff}-gcc" "${_mingw_suff}-binutils" "${_mingw_suff}-crt" "${_mingw_suff}-headers" 'autotools')
depends=("${_mingw_suff}-crt")
optdepends=()
backup=()
options=('!strip' '!buildflags' 'staticlibs' '!emptydirs' '!buildflags')
source=("mingw-w64"::"git+https://github.com/Windows-on-ARM-Experiments/mingw-woarm64.git#branch=woarm64"
        0001-Define-__-de-register_frame_info-in-fake-libgcc_s.patch
        9999-workarounds.patch)
sha256sums=('SKIP'
            'SKIP'
            'SKIP')
msys2_references=(
  'archlinux: mingw-w64-winpthreads'
)

_targets="aarch64-w64-mingw32 x86_64-w64-mingw32"
#_targets="aarch64-w64-mingw32 x86_64-w64-mingw32ucrt i686-w64-mingw32 x86_64-w64-mingw32"

apply_patch_with_msg() {
  for _fname in "$@"
  do
    msg2 "Applying ${_fname}"
    patch -Nbp1 -F0 --verbose -i ${srcdir}/${_fname}
  done
}

pkgver() {
  cd "$srcdir/mingw-w64"

  printf "%s.r%s.%s" "${_base_ver}" "$(git rev-list --count HEAD)" "$(git rev-parse --short=12 HEAD)"
}

prepare() {
  cd "$srcdir/mingw-w64"

  # Remove files created by patches.
  rm -f mingw-w64-libraries/winpthreads/src/libgcc/dll_frame_info.c

  apply_patch_with_msg \
    0001-Define-__-de-register_frame_info-in-fake-libgcc_s.patch \
    9999-workarounds.patch

  cd "${srcdir}"/mingw-w64/mingw-w64-libraries/winpthreads

  autoreconf -vfi

  for _target in ${_targets}; do
    mkdir -p ${srcdir}/build-${_target}
  done
}

build() {
  for _target in ${_targets}; do
    cd ${srcdir}/build-${_target}

    unset CC

    ${srcdir}/mingw-w64/mingw-w64-libraries/winpthreads/configure \
      --prefix=/opt/${_target} \
      --build=${CHOST} \
      --host=${_target} \
      --enable-static \
      --enable-shared
    make
  done
}

package() {
  for _target in ${_targets}; do
    cd ${srcdir}/build-${_target}
    make DESTDIR=${pkgdir} install
  done
}
