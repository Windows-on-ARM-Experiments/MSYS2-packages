# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=windows-default-manifest
pkgver=6.4
pkgrel=2
pkgdesc='Default Windows application manifest'
url='https://cygwin.com/'
msys2_repository_url='https://sourceware.org/git/cygwin-apps/windows-default-manifest.git'
arch=('aarch64' 'i686' 'x86_64')
license=('BSD')
makedepends=('git' 'gcc' 'autotools')
_GIT_TAG="release-${pkgver//\./_}"
source=("git://sourceware.org/git/cygwin-apps/${pkgname}.git#tag=${_GIT_TAG}")
sha256sums=('SKIP')

_targets="aarch64-pc-msys x86_64-pc-msys"
#_targets="aarch64-pc-msys i686-pc-msys x86_64-pc-msys"

apply_patch_with_msg() {
  for _fname in "$@"
  do
    msg2 "Applying ${_fname}"
    patch -Nbp1 -F0 --verbose -i ${srcdir}/${_fname}
  done
}

prepare() {
  cd ${srcdir}/${pkgname}

  autoreconf -fiv
}

build() {
  for _target in ${_targets}; do
    msg "Building ${_target} ${pkgname}"

    [[ -d ${srcdir}/build-${_target} ]] && rm -rf build-${_target}
    cp -rf ${srcdir}/${pkgname} ${srcdir}/build-${_target}

    cd ${srcdir}/build-${_target}

    prefix=/usr
    if [[ ${_target} != ${CARCH}* ]]; then
      prefix=/usr/${_target}
    fi

    local CYGWIN_CHOST="${CHOST/-msys/-cygwin}"

    ${srcdir}/${pkgname}/configure \
      --prefix=${prefix} \
      --build=${CYGWIN_CHOST} \
      --host=${_target} \
      --target=${_target}

    make
  done
}

package() {
  for _target in ${_targets}; do
    msg "Installing ${_target} ${pkgname}"

    cd ${srcdir}/build-${_target}
    make DESTDIR=${pkgdir} install
  done
}
