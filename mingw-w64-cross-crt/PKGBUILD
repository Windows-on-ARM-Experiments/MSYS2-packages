# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Martell Malone <martellmalone@gmail.com>

_realname=crt
_base_ver=12.0.0
_mingw_suff=mingw-w64-cross
pkgname=("${_mingw_suff}-${_realname}")
provides=("${_mingw_suff}-${_realname}-git")
conflicts=("${_mingw_suff}-${_realname}-git")
replaces=("${_mingw_suff}-${_realname}-git")
pkgver=12.0.0
pkgrel=1
pkgdesc='MinGW-w64 CRT for cross-compiler'
arch=('aarch64', 'i686' 'x86_64')
url='https://mingw-w64.sourceforge.io/'
license=('custom')
groups=("${_mingw_suff}-toolchain" "${_mingw_suff}")
depends=("${_mingw_suff}-headers")
makedepends=("git" "${_mingw_suff}-gcc-stage1" "${_mingw_suff}-binutils" 'autotools')
options=('!strip' 'staticlibs' '!emptydirs' '!buildflags')
source=("mingw-w64"::"git+https://github.com/Windows-on-ARM-Experiments/mingw-woarm64.git#branch=woarm64"
        9999-workarounds.patch)
sha256sums=('SKIP'
            'SKIP')
msys2_references=(
  'archlinux: mingw-w64-crt'
  'cpe: cpe:/a:mingw-w64:mingw-w64'
)

_targets="aarch64-w64-mingw32 x86_64-w64-mingw32"
#_targets="aarch64-w64-mingw32 x86_64-w64-mingw32ucrt i686-w64-mingw32 x86_64-w64-mingw32"

apply_patch_with_msg() {
  for _fname in "$@"
  do
    msg2 "Applying ${_fname}"
    patch -Nbp1 -F0 --verbose -i ${srcdir}/${_fname}
  done
}

pkgver() {
  cd "${srcdir}/mingw-w64"

  printf "%s.r%s.%s" "${_base_ver}" "$(git rev-list --count HEAD)" "$(git rev-parse --short=12 HEAD)"
}

prepare() {
  cd "${srcdir}/mingw-w64"

  apply_patch_with_msg \
    9999-workarounds.patch

  for _target in ${_targets}; do
    mkdir -p ${srcdir}/build-${_target}
  done
}

build() {
  for _target in ${_targets}; do
    msg "Building ${_target} ${_realname}"

    case "${_target}" in
      i686*)
        _crt_configure_args="--disable-lib64 --enable-lib32"
      ;;
      x86_64*)
        _crt_configure_args="--disable-lib32 --enable-lib64"
      ;;
      aarch64*)
        _crt_configure_args="--disable-lib32 --disable-lib64 --disable-libarm32 --enable-libarm64"
      ;;
    esac

    cd ${srcdir}/build-${_target}

    if [[ "${_target}" == "x86_64-w64-mingw32ucrt" ]]; then
      _default_msvcrt=ucrt
    else
      _default_msvcrt=msvcrt
    fi

    unset CC

    ${srcdir}/mingw-w64/mingw-w64-crt/configure \
      --prefix=/opt/${_target} \
      --build=${CHOST} \
      --host=${_target} \
      --target=${_target} \
      --enable-wildcard \
      --with-default-msvcrt=${_default_msvcrt} \
      --disable-dependency-tracking \
      ${_crt_configure_args}

    make
  done
}

package() {
  for _target in ${_targets}; do
    msg "Installing ${_target} ${_realname}"
    cd ${srcdir}/build-${_target}
    make DESTDIR=${pkgdir} install
  done
}
